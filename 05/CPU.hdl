// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Central Processing unit (CPU).
 * Consists of an ALU and a set of registers, designed to fetch and 
 * execute instructions written in the Hack machine language.
 * In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM=0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time unit. If reset=1 then the 
 * CPU jumps to address 0 (i.e. sets pc=0 in next time unit) rather 
 * than to the address resulting from executing the current instruction. 
 */


/**
*   Conceptual Collaboration with Joe St'Angelo
*/

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset=1) or continue executing
                         // the current program (reset=0).

    OUT outM[16],        // M value output
        writeM,          // Write into M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

    And(a=instruction[15], b=instruction[5], out=selLastA);
    Mux16(a=instruction, b=loop, sel=selLastA, out=inA);
    Not(in=instruction[15], out=Ainst);
    Or(a=Ainst, b=instruction[5], out=loadA);
    ARegister(in=inA, load=loadA, out=outA, out[0..14]=addressM);

    Mux16(a=outA, b=inM, sel=instruction[12], out=AM);
    
    And(a=instruction[15], b=instruction[4], out=loadD);
    DRegister(in=loop, load=loadD, out=D);

    And(a=instruction[15], b=instruction[3], out=writeM);

    ALU(x=D, y=AM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], zr=isZero, ng=isNeg, out=outM, out=loop);

    And(a=isZero, b=instruction[1], out=jump0);
    And(a=isNeg, b=instruction[2], out=jumpL0);
    Not(in=isNeg, out=notNeg);
    Not(in=isZero, out=notZero);
    And(a=notNeg, b=instruction[0], out=jumpGE);
    And(a=notZero, b=jumpGE, out=jumpG0);


    Or(a=jump0, b=jumpL0, out=aljump);
    Or(a=aljump, b=jumpG0, out=jump);

    And(a=jump, b=instruction[15], out=loadPC);

    PC(in=outA, load=loadPC, inc=true, reset=reset, out[0..14]=pc);
}